0xyPhilic

high

# Incorrect math causes loss for the user

## Summary

high

Swapping from USDT to USD1 allows the user to either set an exact `amountIn` or exact `amountOut` of tokens to receive. The protocol calculates based on the current USD1/USDT price how much the `amountIn` or `amountOut` would be while also incorporating any `buyFee` or `sellFee`. The fee is transferred as addition to the necessary amount of USDT and is later minted in the form of USD1 - basically the user pays for the minting of X + Y USD1, where X is his desired/output amount and Y is the fee that is generated on that amount.

## Vulnerability Detail

The vulnerability appears when a user tries to mint USD1 with USDT while specifying that he/she wants an `exactAmountOut`, i.e. exact amount of USD1 to be received. The initial call is made to the `swap` function, which then internally calls `_getSwapResult`, which makes a call to `_calculateSwapResult` within the `SwapFunctions.sol` to obtain the `amountIn`, `amountOut` and `fee`. 

Within the  `_calculateSwapResult` the call is forwarded to `_calculateSwapResultByAmountOut()` function after the following check:

```solidity
function _calculateSwapResult(SwapRequest memory request)
        internal
        view
        virtual
        returns (uint256 amountIn, uint256 amountOut, uint256 fee)
    {
        _validateFeeFraction(request.feeNumerator, request.feeBase);

        if (request.amountType == AmountType.In) {
            return _calculateSwapResultByAmountIn(request);
        } else {
            return _calculateSwapResultByAmountOut(request);
        }
    }
```
Since the `request.tokenIn` is not the `feeToken` as it is USDT, the `_calculateSwapResultByAmountOut` goes into the following block of code:

```solidity
else {
            // When tokenOut is feeToken, adds the fee before converting the amount
            fee = _getFeeByAmountWithoutFee(amountOut, request.feeNumerator, request.feeBase);
            amountIn = _convert(
                request.tokenOut,
                request.tokenIn,
                amountOut + fee,
                MathUpgradeable.Rounding.Up,
                request.price,
                request.priceBase,
                request.quoteToken
            );
        }
```

The `_convert` function enters into the following `if else` statement:

```solidity
else if (fromToken == quoteToken) {
            return _convertByToPrice(fromToken, toToken, fromAmount, rounding, price, priceBase);
        }
```

And `_convertByToPrice` executes calculations based on the price and amount passed to produce the expected `amountIn` which includes the fee:

```solidity
function _convertByToPrice(
        address fromToken,
        address toToken,
        uint256 fromAmount,
        MathUpgradeable.Rounding rounding,
        uint256 price,
        uint256 priceBase
    ) internal view virtual returns (uint256) {
        uint256 fromBase = 10 ** IERC20Metadata(fromToken).decimals();
        uint256 toBase = 10 ** IERC20Metadata(toToken).decimals();
        return fromAmount.mulDiv(priceBase * toBase, price * fromBase,  rounding);
    }
```

The problem is that the `amountIn` value returned is incorrect and does not respond to the expected `amountIn` The following test can be run to showcase the issue:

```solidity
pragma solidity ^0.8.19;

import "./Unitas.t.sol";


contract IncorrectFee is UnitasTest {

    using MathUpgradeable for uint256;

    address user = vm.addr(0x10);

    function test_incorrectFeeMath() external {
        deal(address(_usdt), address(user), 1000e6);
        deal(address(_usdt), address(_guardian), 1000e6);
        deal(address(_usd1), address(_guardian), 1000e18);
        vm.startPrank(_guardian);
        _usdt.approve(address(_insurancePool), type(uint256).max);
        _insurancePool.depositCollateral(address(_usdt), 1000e6);
        vm.stopPrank();
        vm.startPrank(_timelock);
        ITokenManager.PairConfig[] memory pairs = new ITokenManager.PairConfig[](1);
        pairs[0] = ITokenManager.PairConfig({
            baseToken: address(_usd1),
            quoteToken: address(_usdt),
            buyFee: 10000, // 1% fee
            buyReserveRatioThreshold: 1.3e18,
            sellFee: 10000, // 1% fee
            sellReserveRatioThreshold: 1.3e18
        });
        _tokenManager.updatePairs(pairs);
        vm.stopPrank();
        vm.startPrank(user);
        uint256 initialUSDTBalanceUser = _usdt.balanceOf(user);
        uint256 initialTotalSupplyUSD1 = _usd1.totalSupply();
        uint256 valueOut = 200e18;
        _usdt.approve(address(_unitas), type(uint256).max);
        _unitas.swap(address(_usdt), address(_usd1), ISwapFunctions.AmountType.Out, valueOut);
        vm.stopPrank();
        console.log("Initial USDT Balance of User: ", initialUSDTBalanceUser);
        console.log("Current USDT Balance of User: ", _usdt.balanceOf(user));
        console.log("Initial USD1 Total Supply: ", initialTotalSupplyUSD1);
        console.log("Current USD1 Total Supply: ", _usd1.totalSupply());
        assertEq(_usd1.totalSupply(), valueOut + ((valueOut * 10000) / 10 ** 6));
        assertEq(_usdt.balanceOf(user), 1000e6 - (valueOut / 10 ** 12));
    }
```
As we can see the `buyFee` and `sellFee` is set to 1% for easier calculations. The price of USD1/USDT is also 1:1. Inputting an expected value of 200 USD1 tokens should yield a requirement to pass 202 USDT as follows:
- 200 USDT to be used to mint 200 USD1 (at a 1:1 rate)
- 2 USDT to be paid as fee (used to mint 2 USD1 which are sent to the feeRecipient specified) - 1% of 200 USD1 = 2 USD1 = 2 USDT at 1:1 rate

However the output amounts are greater so the user is practically overcharged in terms of fee.

## Impact

Loss of funds for the user as he pays more fee than he is supposed to.

## Code Snippet

https://github.com/sherlock-audit/2023-04-unitasprotocol/blob/main/Unitas-Protocol/src/SwapFunctions.sol#L227-L239

## Tool used

Manual Review / Foundry

## Recommendation

Most likely the issue comes from the decimal difference (USD1 is 18 and USDT is 6). This issue would persist if other tokens with less than 18 decimals are added as collaterals (ex. USDC).
 
Review and update the math which calculates `amountIn` based on an expected `amountOut` to factor in difference of decimals or create `wrappers` for tokens with less than 18 decimals - i.e. wrap any USDT, USDC to a `uUSDT` or `uUSDC` token that is 1:1 and has 18 decimals.
