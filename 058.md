Ruhum

medium

# Updating USD1 address will leave existing users & protocol with worthless tokens

## Summary
The protocol allows the admin to update the USD1 address. That will render the token worthless because it can't be traded for the underlying asset anymore. It also deletes the existing USD1 tokens from the protocol's liabilities leaving it with a higher reserve ratio than it should.

## Vulnerability Detail
Through the timelock, the admin is able to update the address of USD1:

```sol
    function setUSD1(address token) external onlyTimelock {
        _setUSD1(token);
    }

    function _setUSD1(address token) internal {
        address oldToken = address(usd1);
        if (oldToken != address(0)) {
            _removeToken(oldToken);
        }

        _addToken(token, uint8(TokenType.Stable));
        usd1 = IERC20Token(token);
    }

    function _removeToken(address token) internal override {
        _require(pairTokenLength(token) == 0, Errors.PAIRS_MUST_REMOVED);

        super._removeToken(token);

        if (token == address(usd1)) {
            usd1 = IERC20Token(address(0x0));
        }
    }
```

Since the old token is removed, you won't be able to swap it for any of the other tokens through the Unitas contract. That makes the token worthless since its value depends on the underlying asset it's pegged to.

Also, `_getTotalLiabilities()` won't use the old USD1 supply to calculate the contract's liabilities since it's removed from the TokenManager:

```sol
    function _getTotalLiabilities() internal view returns (uint256 liabilities) {
        address baseToken = address(tokenManager.usd1());
        uint8 tokenTypeValue = uint8(ITokenManager.TokenType.Stable);
        uint256 tokenCount = tokenManager.tokenLength(tokenTypeValue);
        uint256 priceBase = 10 ** oracle.decimals();

        for (uint256 i; i < tokenCount; i++) {
            address token = tokenManager.tokenByIndex(tokenTypeValue, i);
            uint256 tokenSupply = IERC20Token(token).totalSupply();

            if (token == baseToken) {
                // Adds up directly when the token is USD1
                liabilities += tokenSupply;
            } else if (tokenSupply > 0) {
                uint256 price = oracle.getLatestPrice(token);

                liabilities += _convert(
                    token,
                    baseToken,
                    tokenSupply,
                    MathUpgradeable.Rounding.Down,
                    price,
                    priceBase,
                    token
                );
            }
        }
    }
```

The USDT used to mint the old USD1 tokens still exists inside the Unitas contract.

## Impact
By switching USD1 tokens you:
1. leave existing USD1 holders with a worthless token
2. bump up the Unitas contract's reserve ratio because you delete the existing USD1 supply from the liabilities calculation

## Code Snippet
https://github.com/sherlock-audit/2023-04-unitasprotocol/blob/main/Unitas-Protocol/src/TokenManager.sol#L96
https://github.com/sherlock-audit/2023-04-unitasprotocol/blob/main/Unitas-Protocol/src/TokenManager.sol#L223
https://github.com/sherlock-audit/2023-04-unitasprotocol/blob/main/Unitas-Protocol/src/TokenManager.sol#L265
https://github.com/sherlock-audit/2023-04-unitasprotocol/blob/main/Unitas-Protocol/src/Unitas.sol#L540
## Tool used

Manual Review

## Recommendation
Switching USD1 should only be allowed if the existing supply of the old token is 0, or if existing USD1 holders are allowed to migrate to the new token in 1:1 ratio.
